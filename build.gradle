plugins {
    id 'java'
    id("com.github.johnrengelman.shadow") version "8.1.1"
}

group = 'pl.norbit'
version = 'v1.7.1'

compileJava {options.encoding = "UTF-8"}

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        url = 'https://repo.extendedclip.com/releases/'
    }
    maven { url 'https://jitpack.io' }
    maven {
        url = 'https://repo.rosewooddev.io/repository/public/'
    }
    maven {
        url "https://repo.codemc.io/repository/maven-snapshots/"
    }
}

dependencies {
    compileOnly "com.destroystokyo.paper:paper-api:1.16.5-R0.1-SNAPSHOT"

    //playerpoints
    compileOnly 'org.black_ixx:playerpoints:3.2.6'

    //lombok
    compileOnly 'org.projectlombok:lombok:1.18.26'
    annotationProcessor 'org.projectlombok:lombok:1.18.26'

    //depend
    compileOnly 'me.clip:placeholderapi:2.11.6'
    compileOnly "com.github.MilkBowl:VaultAPI:1.7"

    //database
    implementation 'mysql:mysql-connector-java:8.0.33'
    implementation 'commons-dbutils:commons-dbutils:1.7'
    implementation 'com.h2database:h2:2.2.220'

    //gui
    implementation 'com.github.hamza-cskn.obliviate-invs:core:4.2.0'
    implementation 'com.github.hamza-cskn.obliviate-invs:pagination:4.2.0'
    implementation 'com.github.hamza-cskn.obliviate-invs:advancedslot:4.2.0'

    //itemsadder
    compileOnly 'com.github.LoneDev6:API-ItemsAdder:3.6.2-beta-r3-b'

    //yaml
    implementation group: 'org.yaml', name: 'snakeyaml', version: '2.0'

    //anvilgui
    implementation 'net.wesjd:anvilgui:1.10.10-SNAPSHOT'
}

def targetJavaVersion = 11

shadowJar {
    archiveClassifier.set("")
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
